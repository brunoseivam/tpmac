CLOSE

;################################################
; Define motion for 2 slit blades
; Original Author: Tom Cobb
; Used variables: Q71..Q79, Q81..Q89
; Macros (and example values): 
; COORD = 1       ;CS number
; PLC = 16        ;PLC number, should be CS number+15
; BP = 1          ;Axisnum for Blade+
; BM = 3          ;Axisnum for Blade-
;################################################

; Note that we are providing a CS axis with an MRES of 0.001 and EGU the same
; as the underlying axes
; This is needed as the motor record will only move in full steps

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(BP)->I ; +ve blade
#$(BM)->I ; -ve blade

; These are set by mres_reporting_motor.template
#define BPMRES P(100+$(BP))
#define BPOFF  P(300+$(BP))
#define BMMRES P(100+$(BM))
#define BMOFF  P(300+$(BM))

OPEN FORWARD
; Calculate Gap and Centre
; X = Q7 = centre in mm = ({B+}+{B-})/2
; Y = Q8 = gap in mm = {B+}-{B-} 
; Note that we divide by 0.0001 to give CS mres of 0.0001
; Also note the use of mres and offset in the calculation
CLEAR
Q7=(BPMRES*P$(BP)+BPOFF+BMMRES*P$(BM)+BMOFF)/0.0002
Q8=(BPMRES*P$(BP)+BPOFF-BMMRES*P$(BM)-BMOFF)/0.0001
CLOSE

OPEN INVERSE
; P$(BP) = B+ = {centre}+({gap}/2)
; P$(BM) = B- = {centre}-({gap}/2)
CLEAR
P$(BP)=(Q7*0.0001+Q8*0.00005-BPOFF)/BPMRES
P$(BM)=(Q7*0.0001-Q8*0.00005-BMOFF)/BMMRES
CLOSE

; A PLC(sx+15) needs to be made to do position reporting
; Readbacks should be in &{axisnum}Q81..89 
OPEN PLC$(PLC)
CLEAR
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define BPVAL m$(BP)62/(I$(BP)08*32)
#define BMVAL m$(BM)62/(I$(BM)08*32)
; Put velocity into Q9x
#define BMVEL m$(BM)66/(I$(BM)09*32)
#define BPVEL m$(BP)66/(I$(BP)09*32)
ADDRESS&$(COORD)
Q87=(BPMRES*BPVAL+BPOFF+BMMRES*BMVAL+BMOFF)/0.0002
Q88=(BPMRES*BPVAL+BPOFF-BMMRES*BMVAL-BMOFF)/0.0001
Q97=(BMMRES*BMVEL+BPMRES*BPVEL)/0.0002
Q98=(BPMRES*BPVEL-BMMRES*BMVEL)/0.0001
CLOSE
ENABLE PLC$(PLC)
