CLOSE

;###############################################
; Define motion for Energy in Accel DCM
; Original Author: Tom Cobb
; Used variables: Q71..Q79, Q81..Q89, BMRES..TOFF, Q20, P$(PLC)00
; Macros (and example values): 
; COORD = 1          ;CS number (only works for CS 1..9)
; PLC = 16           ;PLC number, should be CS number+15
; BRAGG = 2          ;Axisnum for Bragg
; T2 = 3             ;Axisnum for T2
;################################################

; Note that we are providing a CS axis with an MRES of 0.001 and EGU the same
; as the underlying axes
; This is needed as the motor record will only move in full steps

; Change to CS$(COORD)
&$(COORD)

; Set relevant axes to use kinematics
#$(BRAGG)->I
#$(T2)->I

; Trig in degrees
i15=0

; These are set by mres_reporting_motor.template
#define BMRES P(100+$(BRAGG))
#define BOFF  P(300+$(BRAGG))
#define TMRES P(100+$(T2))
#define TOFF  P(300+$(T2))

; Setup energy calculation variables
Q110=12.3985 ; Evlambda
Q111=3.1355  ; d = crystal spacing in angstrom

&1 OPEN FORWARD
; This kinematic is for the DCM energy
; need to multiply everything by 10000 to give decent MRES value
; X = Q7 = energy in kEv = {Evlamda}/(2*{d}*sin(Bragg))
; Y = Q8 = offset in mm = 2*(T)*cos(Bragg)
CLEAR
Q20=2*Q111*SIN(BMRES*P$(BRAGG)+BOFF)
IF (Q20>0.01)
    Q7=10000*Q110/(Q20)
    Q8=20000*(TMRES*P$(T2)+TOFF)*COS(BMRES*P$(BRAGG)+BOFF)
ELSE
    M5$(COORD)82=1
ENDIF
CLOSE

&1 OPEN INVERSE
CLEAR
; P$(BRAGG) = Bragg = arcsin({Evlamda}/(2*{d}*(Energy)) or arcsin((wavelength)/(2*{d}))
; P$(T2) = T = (Offset)/2*cos(Bragg)
; TODO: find out why we need 0.1% fudge factor in P$(T2) calculation?
Q20=10000*Q110/(2*Q111*Q7)
IF (ABS(Q20)<1)
    P$(BRAGG)=(ASIN(Q20)-BOFF)/BMRES
    P$(T2)=(Q8/(20002*COS(ASIN(Q20)))-TOFF)/TMRES
ELSE
    M5$(COORD)82=1
ENDIF
CLOSE

OPEN PLC $(PLC)
; PLC for position reporting
CLEAR
; As forward kinematic, but with Px = mx62/(Ix08*32)
#define BVAL m$(BRAGG)62/(I$(BRAGG)08*32)
#define TVAL m$(T2)62/(I$(T2)08*32)
ADDRESS&$(COORD)
P$(PLC)00=2*Q111*SIN(BMRES*BVAL+BOFF)
IF (P$(PLC)00>0.01)
    Q87=10000*Q110/P$(PLC)00
    Q88=20000*(TMRES*TVAL+TOFF)*COS(BMRES*BVAL+BOFF)
ELSE
    M5$(COORD)82=1
ENDIF
CLOSE
ENABLE PLC $(PLC)


