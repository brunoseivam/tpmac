; Program-12: one motor, position (backlash from one side only)
; PMAC-2 version
; (for Ram6X2 -- GM/CA CAT)
;
;----------------------------------------;
; $(assy)AccelTime  -> TA0   -> M(Q71)   ;
; $(assy)AccelCurve -> TS0   -> M(Q72)   ;
;       0           -> F0    -> M(Q73)   ; [was: $(assy)FeedRateSlw <- $(m1)SelSlw = 0]
;       1           -> FRAX0 -> M(Q74)   ; [was: $(assy)FeedRateAxis]
; $(m1)RqsPos       -> X0    -> M(Q75)   ;
; $(m1)RqsBkl       -> X1    -> M(Q76)   ;
;----------------------------------------;
;        v--PMAC2
; Use: Q70*100+5047,  Q71, Q72, Q73, Q75, Q76
;       ${m1}DesPos   TA0, TS0, F0,   X0,  X1
; Q70    is the coordinate system number.
; DesPos is nearly the same as ActPos - this is the position
;        around which PMAC is currently trying to hold the motor.
;
CLOSE
;
OPEN PROG 12 CLEAR
  TA(M(Q71+0))	;set acceleration time
  TS(M(Q72+0))	;set s-curve part of acceleration time
  FRAX(X)	;specify feedrate axes -- is not used here
  F(M(Q73+0))	;set feedrate (speed)  -- is not used here
  LINEAR
  ABS
  Q1=M(Q76+0)				;RqsBkl
  Q2=M(Q75+0)-M(Q70*100+5047)		;RqsPos-DesPos
  IF( Q1>0 AND Q2<0 OR Q1<0 AND Q2>0)
    X(M(Q75+0)-M(Q76+0))		;goto RqsPos-RqsBkl
    DWELL0
    X(M(Q75+0))				;goto RqsPos
  ELSE
    X(M(Q75+0))				;goto RqsPos
  ENDIF
CLOSE
