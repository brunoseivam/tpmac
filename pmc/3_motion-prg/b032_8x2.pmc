; Program-32: two motors, backlash (backlash from one side only)
; PMAC-2 version
; (for Ram8X2 -- GM/CA CAT)
;
;----------------------------------------;
; $(assy)AccelTime  -> TA0   -> M(Q71)   ;
; $(assy)AccelCurve -> TS0   -> M(Q72)   ;
;       0           -> F0    -> M(Q73)   ; [was: $(assy)FeedRateSlw <- $(m1)SelSlw = 0]
;       1           -> FRAX0 -> M(Q74)   ; [was: $(assy)FeedRateAxis]
; $(m1)RqsPos       -> X0    -> M(Q75)   ;
; $(m1)RqsBkl       -> X1    -> M(Q76)   ;
; $(m2)RqsPos       -> Y0    -> M(Q77)   ;
; $(m2)RqsBkl       -> Y1    -> M(Q78)   ;
;----------------------------------------;
;         v---PMAC1---v
; Use: Q70*100+5047,  Q70*100+5048,  Q71, Q72, Q73, Q74,  Q75, Q76, Q77, Q78
;      ${m1}DesPos  ${m2}DesPos, TA0, TS0, F0, FRAX0,  X0,  X1,  Y0,  Y1
; Q70    is the coordinate system number.
; DesPos is nearly the same as ActPos - this is the position
;        around which PMAC is currently trying to hold the motor.
;
CLOSE
;
OPEN PROG 32 CLEAR
  TA(M(Q71+0))		;set acceleration time
  TS(M(Q72+0))		;set s-curve part of acceleration time
  FRAX(X,Y)		;specify feedrate axes -- is not used here
  Q9=M(Q74+0)
  IF(Q9=1)FRAX(X)
  IF(Q9=2)FRAX(Y)
  IF(Q9=3)FRAX(X,Y)
  F(M(Q73+0))		;set feedrate (speed)  -- is not used here
  LINEAR
  ABS
  Q1=M(Q76+0)                                   ;${m1}RqsBkl
  Q2=M(Q75+0)-M(Q70*100+5047)			;${m1}RqsPos-${m1}DesPos
  Q3=M(Q78+0)					;${m2}RqsBkl
  Q4=M(Q77+0)-M(Q70*100+5048)			;${m2}RqsPos-${m2}DesPos
  IF( Q1>0 AND Q2<0 OR Q1<0 AND Q2>0)
    IF( Q3>0 AND Q4<0 OR Q3<0 AND Q4>0)
      X(M(Q75+0)-M(Q76+0))Y(M(Q77+0)-M(Q78+0))	;goto ${m1}RqsPos-${m1}RqsBkl & ${m2}RqsPos-${m2}RqsBkl
      DWELL0
      X(M(Q75+0))Y(M(Q77+0))			;goto ${m1}RqsPos & ${m2}RqsPos
    ELSE
      X(M(Q75+0)-M(Q76+0))Y(M(Q77+0))		;goto ${m1}RqsPos-${m1}RqsBkl & ${m2}RqsPos
      DWELL0
      X(M(Q75+0))				;goto ${m1}RqsPos
    ENDIF
  ELSE
    IF( Q3>0 AND Q4<0 OR Q3<0 AND Q4>0)
      X(M(Q75+0))Y(M(Q77+0)-M(Q78+0))		;goto ${m1}RqsPos & ${m2}RqsPos-${m2}RqsBkl
      DWELL0
      Y(M(Q77+0))				;goto ${m2}RqsPos
    ELSE
      X(M(Q75+0))Y(M(Q77+0))			;goto ${m1}RqsPos & ${m2}RqsPos
    ENDIF
  ENDIF
CLOSE
